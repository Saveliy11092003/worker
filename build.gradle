plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.3'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'ru.trushkov'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

configurations {
	jaxb
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-web-services'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	implementation 'wsdl4j:wsdl4j'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.16.1'

	implementation 'org.glassfish.jaxb:jaxb-xjc'
	jaxb 'org.glassfish.jaxb:jaxb-xjc:4.0.3'
	jaxb 'org.glassfish.jaxb:jaxb-runtime:4.0.3'

	implementation 'com.github.dpaukov:combinatoricslib3:3.4.0'

	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'
	implementation 'com.fasterxml.jackson.module:jackson-module-jaxb-annotations'
}

jar {
	manifest {
		attributes(
				'Main-Class': 'ru.trushkov.worker.WorkerApplication'
		)
	}
}

bootJar {
	archiveBaseName.set('gs-producing-web-service')
	archiveVersion.set('0.1.0')
}

sourceSets {
	main {
		java {
			srcDir 'src/main/java'
			srcDir 'build/generated-sources/jaxb'
		}
	}
}

task genJaxb {
	ext.sourcesDir = "${buildDir}/generated-sources/jaxb"
	ext.schema1 = "src/main/resources/crack_hash_manager_schema.xsd"
	ext.schema2 = "src/main/resources/crack_hash_worker_schema.xsd"

	outputs.dir sourcesDir

	doLast {
		// Создайте директории для каждой схемы
		def schema1Dir = "${sourcesDir}/schema1"
		def schema2Dir = "${sourcesDir}/schema2"

		mkdir(schema1Dir)
		mkdir(schema2Dir)

		project.ant {
			taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
					classpath: configurations.jaxb.asPath

			// Генерация для первой схемы
			xjc(destdir: schema1Dir, schema: schema1) {
				arg(value: "-wsdl")
				produces(dir: schema1Dir, includes: "**/*.java")
			}

			// Генерация для второй схемы
			xjc(destdir: schema2Dir, schema: schema2) {
				arg(value: "-wsdl")
				produces(dir: schema2Dir, includes: "**/*.java")
			}
		}
	}
}

compileJava.dependsOn genJaxb

tasks.named('test') {
	useJUnitPlatform()
}
